import org.apache.avro.tool.SpecificCompilerTool

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        // Add the Avro code generation to the build dependencies so that it can be used in a Gradle task.
        classpath group: 'org.apache.avro', name: 'avro-tools', version: '1.11.1'
    }
}

plugins {
    id 'java'
    id 'com.google.cloud.tools.jib' version '1.1.1'
    id 'com.github.spotbugs' version '4.6.0'
    id 'checkstyle'
    id 'maven-publish'
    id "com.gradleup.shadow" version "8.3.5"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

version = '0.0.1'
group = 'com.friendsdrinksv2'

repositories {
    mavenCentral()
    jcenter()

    maven {
        url 'https://packages.confluent.io/maven'
    }
}

apply plugin: 'com.gradleup.shadow'

checkstyle {
    toolVersion '7.8.1'
    configFile file("checkstyle/checkstyle.xml")
}
checkstyleMain {
    source ='src/main/java'
}
checkstyleTest {
    source ='src/test/java'
}

dependencies {
    implementation 'org.apache.avro:avro:1.11.1'
    implementation 'org.apache.logging.log4j:log4j-core:2.13.3'
    implementation 'org.apache.logging.log4j:log4j-api:2.13.3'
    implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.13.3'
    implementation 'org.apache.kafka:kafka-streams:3.3.1'
    implementation 'io.confluent:kafka-streams-avro-serde:7.3.1'
    implementation 'org.eclipse.jetty:jetty-server:9.4.39.v20210325'
    implementation 'org.eclipse.jetty:jetty-servlet:9.4.39.v20210325'
    implementation 'org.eclipse.jetty:jetty-servlets:9.4.39.v20210325'
    implementation 'org.glassfish.jersey.core:jersey-server:2.34'
    implementation 'org.glassfish.jersey.media:jersey-media-json-jackson:2.34'
    implementation 'org.glassfish.jersey.containers:jersey-container-servlet-core:2.34'
    implementation 'org.glassfish.jersey.containers:jersey-container-servlet:2.34'
    implementation 'org.glassfish.jersey.inject:jersey-hk2:2.34'
    implementation 'javax.ws.rs:javax.ws.rs-api:2.1.1'
    testImplementation 'junit:junit:4.12'
}

test {
    testLogging {
        outputs.upToDateWhen { false }
        showStandardStreams = false
        exceptionFormat = 'full'
    }
}

publishing {
    publications {
        mavenApp(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            name = 'localRepo'
            url = layout.buildDirectory.dir("repo")
        }
    }
}

def avroSchemasDir = "src/main/avro/friendsdrinks"
def avroCodeGenerationDir = "build/generated-main-avro-java"

// Add the generated Avro Java code to the Gradle source files.
sourceSets.main.java.srcDirs += [avroCodeGenerationDir]

tasks.register('avroCodeGeneration') {
    // Define the task inputs and outputs for the Gradle up-to-date checks.
    inputs.dir(avroSchemasDir)
    outputs.dir(avroCodeGenerationDir)
    // The Avro code generation logs to the standard streams. Redirect the standard streams to the Gradle log.
    logging.captureStandardOutput(LogLevel.INFO);
    logging.captureStandardError(LogLevel.ERROR)
    doLast {
        // Run the Avro code generation.
        new SpecificCompilerTool().run(System.in, System.out, System.err, List.of(
                "-encoding", "UTF-8",
                "-string",
                "-fieldVisibility", "private",
                "-noSetters",
                "schema", "$projectDir/$avroSchemasDir".toString(), "$projectDir/$avroCodeGenerationDir".toString()
        ))
    }
}

tasks.withType(JavaCompile).configureEach {
    // Make Java compilation tasks depend on the Avro code generation task.
    dependsOn('avroCodeGeneration')
}

